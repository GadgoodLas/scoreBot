import java.util.Properties
import java.io.FileInputStream

plugins {
    id("com.android.application")
    id("kotlin-android")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

val keystoreProperties = Properties()
val keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    val rawProps = Properties()
    FileInputStream(keystorePropertiesFile).use { rawProps.load(it) }
    for ((k, v) in rawProps) {
        val cleanKey = k.toString().replace("\uFEFF", "").trim()
        val cleanVal = v.toString().trim()
        keystoreProperties.setProperty(cleanKey, cleanVal)
    }
}

android {
    namespace = "com.scorebot.score_bot"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = "28.2.13676358"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    defaultConfig {
        applicationId = "com.scorebot.score_bot"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    flavorDimensions += "device"
    productFlavors {
        create("phone") {
            dimension = "device"
            versionCode = 101
        }
        create("watch") {
            dimension = "device"
            versionCode = 102
        }
    }

    packaging {
        jniLibs {
            keepDebugSymbols += "**/*.so"
        }
    }

    signingConfigs {
        create("release") {
            val keyAliasVal = keystoreProperties.getProperty("keyAlias")
            val keyPasswordVal = keystoreProperties.getProperty("keyPassword")
            val storeFileVal = keystoreProperties.getProperty("storeFile")
            val storePasswordVal = keystoreProperties.getProperty("storePassword")

            if (storeFileVal != null && file(storeFileVal).exists()) {
                keyAlias = keyAliasVal
                keyPassword = keyPasswordVal
                storeFile = file(storeFileVal)
                storePassword = storePasswordVal
            } else {
                // Fallback debug si le keystore release n'est pas encore généré
                val debugSigning = signingConfigs.getByName("debug")
                keyAlias = debugSigning.keyAlias
                keyPassword = debugSigning.keyPassword
                storeFile = debugSigning.storeFile
                storePassword = debugSigning.storePassword
            }
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.getByName("release")
            isMinifyEnabled = false
            isShrinkResources = false
        }
    }
}

flutter {
    source = "../.."
}
